# Find dotnet cli
find_program(DOTNET_CLI NAMES dotnet)
if(NOT DOTNET_CLI)
    message(FATAL_ERROR "Check for dotnet Program: not found")
else()
    message(STATUS "Found dotnet Program: ${DOTNET_CLI}")
endif()

set(CSPROJ_BINARY_PATH ${CPU_FEATURES_DOTNET}.${CPU_FEATURES_DOTNET_PROCESSOR_OUTPUT_NAME}${CPU_FEATURES_DOTNET_LIBRARY_EXTENSION})

set(PLATFORM_TARGET X64)
set(PREFER_32BIT false)
set(IS_OPTIMIZE false)

if (NOT CPU_FEATURES_DOTNET_PACKAGE_OUTPUT_PATH)
    set(CPU_FEATURES_DOTNET_PACKAGE_OUTPUT_PATH "${PROJECT_BINARY_DIR}/dotnet/packages")
endif ()
set(CPU_FEATURES_DOTNET_NATIVE_LIB_PATH runtimes/${CPU_FEATURES_DOTNET_RID}/native/${CSPROJ_BINARY_PATH})

file(MAKE_DIRECTORY ${CPU_FEATURES_DOTNET_PACKAGE_OUTPUT_PATH})

#
#  CpuFeaturesDotNet Runtime Package
#

set(CPU_FEATURES_DOTNET_RUNTIME_NUGET ${CPU_FEATURES_DOTNET_NAME}.runtime.${CPU_FEATURES_DOTNET_RID})
set(CPU_FEATURES_DOTNET_RUNTIME ${CPU_FEATURES_DOTNET_NAME}.runtime.${CPU_FEATURES_DOTNET_RID})
set(CPU_FEATURES_DOTNET_RUNTIME_PROJECT ${CPU_FEATURES_DOTNET_NAME}.csproj)

configure_file(
        ../cmake/templates/${CPU_FEATURES_DOTNET_NAME}.runtime.csproj.in
        ${CMAKE_CURRENT_SOURCE_DIR}/CpuFeaturesDotNet/${CPU_FEATURES_DOTNET_RUNTIME_PROJECT}
        @ONLY)

add_custom_command(
        OUTPUT ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp
        COMMAND ${DOTNET_CLI} build -c Release ${CPU_FEATURES_DOTNET_RUNTIME_PROJECT}
        COMMAND ${DOTNET_CLI} pack -c Release ${CPU_FEATURES_DOTNET_RUNTIME_PROJECT} -p:PackageId=${CPU_FEATURES_DOTNET_RUNTIME_NUGET}
        COMMAND ${CMAKE_COMMAND} -E make_directory ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}
        COMMAND ${CMAKE_COMMAND} -E touch ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp
        DEPENDS
            ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}/${CPU_FEATURES_DOTNET_RUNTIME_PROJECT}
            ${CPU_FEATURES_DOTNET}
        BYPRODUCTS
            ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}/${CPU_FEATURES_DOTNET_RUNTIME_PROJECT}/bin
            ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}/${CPU_FEATURES_DOTNET_RUNTIME_PROJECT}/obj
        COMMENT "Generate ${CPU_FEATURES_DOTNET_RUNTIME} package (${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp)"
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}
)

add_custom_target(cpu_features_dotnet_runtime_package
        DEPENDS
            ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}
)

#
#  Install CpuFeaturesDotnet runtime package to tests and samples projects
#

configure_file(
        ../cmake/templates/Nuget.Config.in
        ${CMAKE_CURRENT_SOURCE_DIR}/../NuGet.Config
)

set(LOG_SAMPLES_FILE "${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/log_samples.txt")
set(LOG_TESTS_FILE "${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/log_tests.txt")

add_custom_command(OUTPUT ${LOG_SAMPLES_FILE}
        COMMAND ${DOTNET_CLI} add package ${CPU_FEATURES_DOTNET_RUNTIME_NUGET} -s ${CPU_FEATURES_DOTNET_PACKAGE_OUTPUT_PATH} --no-restore
        DEPENDS ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/../samples/CpuFeaturesDotNet.Samples
)

add_custom_command(OUTPUT ${LOG_TESTS_FILE}
        COMMAND ${DOTNET_CLI} add package ${CPU_FEATURES_DOTNET_RUNTIME_NUGET} -s ${CPU_FEATURES_DOTNET_PACKAGE_OUTPUT_PATH} --no-restore
        DEPENDS ${PROJECT_BINARY_DIR}/dotnet/${CPU_FEATURES_DOTNET_RUNTIME}/timestamp
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/../tests
)

add_custom_target(add_runtime_nuget_package_to_tests
        DEPENDS ${LOG_SAMPLES_FILE} ${LOG_TESTS_FILE}
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${CPU_FEATURES_DOTNET_NAME}
)
